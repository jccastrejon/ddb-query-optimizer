options {
	STATIC=true;
    MULTI=true;
    NODE_EXTENDS="mx.itesm.ddb.util.SqlNode";
    NODE_PACKAGE="mx.itesm.ddb.parser";
    TRACK_TOKENS=true;
    IGNORE_CASE=true;
    VISITOR=true;
}

PARSER_BEGIN(SqlParser)

package mx.itesm.ddb.parser; 

import java.util.List;
import java.util.ArrayList;
 
import mx.itesm.ddb.util.QueryData;
import mx.itesm.ddb.util.RelationData;
import mx.itesm.ddb.util.ConditionData;
import mx.itesm.ddb.util.impl.SimpleRelationData;
import mx.itesm.ddb.util.impl.QueryRelationData;

/**
 * SQL Parser.
 * 
 * @author jccastrejon
 * 
 */ 
public class SqlParser {
}

PARSER_END(SqlParser)

/* White spaces */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* SQL Select keywords */
TOKEN :
{
	<K_ALL:"ALL">
|   <K_AND:"AND">
|   <K_ANY:"ANY">
|   <K_AS:"AS">
|   <K_ASC:"ASC">
|	<K_BETWEEN:"BETWEEN">
|   <K_DESC:"DESC">
|   <K_DISTINCT:"DISTINCT">
|   <K_EXISTS:"EXISTS">
|   <K_FROM:"FROM">
|	<K_IN:"IN">
|	<K_IS:"IS">
|   <K_LIKE:"LIKE">
|   <K_NOT:"NOT">
|   <K_NULL:"NULL">
|   <K_OR:"OR">
|   <K_SELECT:"SELECT">
|   <K_WHERE:"WHERE">
}

/* Numeric constants */
TOKEN :
{
	< S_NUMBER: <FLOAT>
	    | <FLOAT> ( ["e","E"] ([ "-","+"])? <FLOAT> )?>
| 	< #FLOAT: <INTEGER>
	    | <INTEGER> ( "." <INTEGER> )?
	    | "." <INTEGER>>
| 	< #INTEGER: ( <DIGIT> )+ >
| 	< #DIGIT: ["0" - "9"] >
}

/* Identifiers */
TOKEN:
{
    < S_IDENTIFIER: (<LETTER>)+ (<DIGIT> | <LETTER> |<SPECIAL_CHARS>)* >
|	< #LETTER: ["a"-"z", "A"-"Z"] >
|	< #SPECIAL_CHARS: "$" | "_" | "#">
|	< S_CHAR_LITERAL: "'" (~["'"])* "'" ("'" (~["'"])* "'")*>
}

/**
 * Represents a SQL code block.
 */
QueryData QueryStatement():
{
	QueryData returnValue;
}
{
	returnValue = Query()
	";"
    {
    	return returnValue;
    }
}

/**
 * SQL query.
 */
QueryData Query() :
{
	QueryData returnValue;
	List<String> attributes;
	List<RelationData> relations;
	String conditions = null;
}
{
    "SELECT" [ "ALL" | "DISTINCT" ] 
    attributes = SelectList()
    relations = FromClause()
    [
    	conditions = WhereClause()
    ]
    {
    	returnValue = new QueryData(attributes, relations, conditions);
    	return returnValue;
    }
}

/*
 * Result attributes.
 */
List<String> SelectList():
{
	List<String> returnValue = new ArrayList<String>();
	String attribute;	
}
{
	(
	    "*"
	    {
	    	returnValue.add("*");
	    } 
	    | attribute = SelectItem()
	    {
	    	returnValue.add(attribute);
	    }
	   	(
	   		"," attribute = SelectItem()
	   		{
	   			returnValue.add(attribute);
	   		}
	   	)*
   	)
    {
    	return returnValue;
    }
}

String SelectItem():
{
	String returnValue = null;
	Token token = null;
}
{
	(
	    (
			LOOKAHEAD(2) token = <S_IDENTIFIER>".*" // table.*
			{
				returnValue = token.image + ".*";
			}
			| returnValue = SQLSimpleExpression() // column name or expression
	    )
	    [ 
	    	[
	    		"AS"
	    		{
	    			returnValue += "AS";
	    		}
	    	]
	    	token = <S_IDENTIFIER>
	    	{
	    		returnValue += token.image;
	    	}
	    ]
	)
	{
		return returnValue;
	}
}

String SQLSimpleExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
		expression = SQLMultiplicativeExpression()
		{
			returnValue.append(expression);
		}
		(
			(
				"+"
				{
					returnValue.append("+");
				}
				| "-"
				{
					returnValue.append("-");
				}
				| "||"
				{
					returnValue.append("||");
				}
			) 
			expression = SQLMultiplicativeExpression()
			{
				returnValue.append(expression);
			}
		)*
	)
	{
		return returnValue.toString();
	}
}


String SQLMultiplicativeExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
		expression = SQLUnaryExpression()	//TODO: exponent?
		{
			returnValue.append(expression);
		}
		(
			(
				"*"
				{
					returnValue.append("*");
				}
				| "/"
				{
					returnValue.append("/");
				}
				| "="
				{
					returnValue.append("=");
				}
			)
			expression = SQLUnaryExpression()	//TODO: exponent?
			{
				returnValue.append(expression);
			}
		)*
	)
	{
		return returnValue.toString();
	}
}

String SQLUnaryExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    [
	    	"+"
	    	{
	    		returnValue.append("+");
	    	}
	    	| "-"
	    	{
	    		returnValue.append("-");
	    	}
	    ] 
	    expression = SQLPrimaryExpression()
	    {
	    	returnValue.append(expression);
	    }
	)
	{
		return returnValue.toString();
	}
}

String SQLPrimaryExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
	Token token;
	QueryData queryData;
}
{
	(
	    token = <S_NUMBER>
	    {
	    	returnValue.append(token.image);
	    }
		| token = <S_CHAR_LITERAL>
	    {
	    	returnValue.append(token.image);
	    }
		| token = <S_IDENTIFIER>
	    {
	    	returnValue.append(token.image);
	    }
		| "NULL"
	    {
	    	returnValue.append("NULL");
	    }
		| "("
	    {
	    	returnValue.append("(");
	    }
		(
			LOOKAHEAD(3) queryData = Query()
			{
				returnValue.append(queryData.toString());	//TODO: Fix this!!:.
			}
			| expression = SQLExpression()
			{
				returnValue.append(expression);
			}
		)
		")"
		{
			returnValue.append(")");
		}
	)
	{
		return returnValue.toString();
	}
}

String SQLExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    expression = SQLAndExpression()
	    {
	    	returnValue.append(expression);
	    } 
	    (
	    	"OR"
	    	{
	    		returnValue.append("OR");
	    	}
	    	expression = SQLAndExpression()
	    	{
	    		returnValue.append(expression);
	    	}
	    )*
	)
	{
		return returnValue.toString();
	}
}

String SQLAndExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    expression = SQLUnaryLogicalExpression()
	    {
	    	returnValue.append(expression);
	    }
	    (
	    	"AND"
	    	{
	    		returnValue.append("AND");
	    	}
	    	expression = SQLUnaryLogicalExpression()
	    	{
	    		returnValue.append(expression);
	    	}
	    )*
	)
	{
		return returnValue.toString();
	}
}

String SQLUnaryLogicalExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    LOOKAHEAD(2) expression = ExistsClause()
	    {
	    	returnValue.append(expression);
	    }
		|(
			[
				"NOT"
				{
					returnValue.append("NOT");
				}
			]
			expression = SQLRelationalExpression()
			{
				returnValue.append(expression);
			}
		)
	)
	{
		return returnValue.toString();
	}
}

String ExistsClause():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
	QueryData queryData;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		returnValue.append("NOT");
	    	}
	    ] 
	    "EXISTS"
	    {
	    	returnValue.append("EXISTS");
	    }
	    "("
	    {
	    	returnValue.append("(");
	    } 
	    queryData = Query()
	    {
	    	returnValue.append(queryData.toString()); //TODO: Fix this!!:.
	    } 
	    ")"
	    {
	    	returnValue.append(")");
	    }
	)
	{
		return returnValue.toString();
	}
}

String SQLRelationalExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
(
    /* Only after looking past "(", Expression() and "," we will know that
    it is expression list */
    
    (
        LOOKAHEAD(
            "("
            {
                returnValue.append("(");
            }
            expression = SQLSimpleExpression()
            {
                returnValue.append(expression);
            }
            ","
            {
                returnValue.append(",");
            }
        )
    
        "("
        {
            returnValue.append("(");
        }
        expression = SQLExpressionList()
        {
            returnValue.append(expression);
        }
        ")"
        {
            returnValue.append(")");
        }
        | 
        (
            expression = SQLSimpleExpression()
            {
                returnValue.append(expression);
            }
        )
    )
    
    /* Lookahead(2) is required because of NOT IN,NOT BETWEEN and NOT LIKE */
    (
        expression = SQLRelationalOperatorExpression()
        {
            returnValue.append(expression);
        }
        | LOOKAHEAD(2) (expression = SQLInClause())
        {
            returnValue.append(expression);
        }
        | LOOKAHEAD(2) (expression = SQLBetweenClause())
        {
            returnValue.append(expression);
        }
        | LOOKAHEAD(2) (expression = SQLLikeClause())
        {
            returnValue.append(expression);
        }
        | expression = IsNullClause()
        {
            returnValue.append(expression);
        }
    )?
)
	{
		return returnValue.toString();
	}
}

String SQLExpressionList():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    expression = SQLExpression()
	    {
	    	returnValue.append(expression);
	    }
	    (
	    	","
	    	{
	    		returnValue.append(",");
	    	}
	    	expression = SQLExpression()
	    	{
	    		returnValue.append(expression);
	    	}
	    )*
	)
	{
		return returnValue.toString();
	}
}

String SQLRelationalOperatorExpression():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
	QueryData queryData;
}
{
    /* Only after seeing an ANY/ALL or "(" followed by a SubQuery() we can
    determine that is is a sub-query
    */
    (   LOOKAHEAD(
			"ANY"
			{
				returnValue.append("ANY");
			}
			| "ALL"
			{
				returnValue.append("ALL");
			}
			| 
				"("
				{
					returnValue.append("(");
				} 
				"SELECT"
				{
					returnValue.append("SELECT");
				}
    	)
        (
        	[
        		"ALL"
        		{
        			returnValue.append("ALL");
        		}
        		| "ANY"
        		{
        			returnValue.append("ANY");
        		}
        	] 
        	"("
        	{
        		returnValue.append("(");
        	} 
        	queryData = Query()
        	{
        		returnValue.append(queryData.toString());	//TODO: Fix this!!:.
        	} 
        	")"
        	{
        		returnValue.append(")");
        	}
        )
    )
	{
		return returnValue.toString();
	}
}

String SQLInClause():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
	QueryData queryData;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		returnValue.append("NOT");
	    	}
	    ] 
	    "IN"
	    {
	    	returnValue.append("IN");
	    } 
	    "("
	    {
	    	returnValue.append("(");
	    } 
	    (
	    	LOOKAHEAD(3) queryData = Query()
	    	{
	    		returnValue.append(queryData.toString());	//TODO: Fix this!!:.
	    	} 
	    	| expression = SQLExpressionList()
	    	{
	    		returnValue.append(expression);
	    	}
	    ) 
	    ")"
	    {
	    	returnValue.append(")");
	    }
	)
	{
		return returnValue.toString();
	}
}

String SQLBetweenClause():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		returnValue.append("NOT");
	    	}
	    ]
	    "BETWEEN"
	    {
	    	returnValue.append("BETWEEN");
	    } 
	    expression = SQLSimpleExpression()
	    {
	    	returnValue.append(expression);
	    } 
	    "AND"
	    {
	    	returnValue.append("AND");
	    } 
	    expression = SQLSimpleExpression()
	    {
	    	returnValue.append(expression);
	    }
	)
	{
		return returnValue.toString();
	}
}

String SQLLikeClause():
{
	StringBuilder returnValue = new StringBuilder();
	String expression;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		returnValue.append("NOT");
	    	}
	    ] 
	   	"LIKE"
	   	{
	   		returnValue.append("LIKE");
	   	}
	   	expression = SQLSimpleExpression()
	   	{
	   		returnValue.append(expression);
	   	}
	)
	{
		return returnValue.toString();
	}
}

String IsNullClause():
{
	StringBuilder returnValue = new StringBuilder();
}
{
	(
	    "IS"
	    {
	    	returnValue.append("IS");
	    }
	    [
	    	"NOT"
	    	{
	    		returnValue.append("NOT");
	    	}
	    ]
	    "NULL"
	    {
	    	returnValue.append("NULL");
	    }
	)
	{
		return returnValue.toString();
	}
}

List<RelationData> FromClause():
{
	List<RelationData> returnValue = new ArrayList<RelationData>();
	RelationData relation;
}
{
    "FROM" 
    relation = QueryTableExpression()
    {
    	returnValue.add(relation);
    }
    ( 
    	"," relation = QueryTableExpression()
    	{
    		returnValue.add(relation);
    	}
    )*
    {
    	return returnValue;
    }
}

RelationData QueryTableExpression():
{
	RelationData returnValue = null;
	Token token = null;
	QueryData queryData = null;
}
{
    (
		token = <S_IDENTIFIER>
		| LOOKAHEAD(3) "(" queryData = Query() ")"
    )
    {
		if(token != null){
			returnValue = new SimpleRelationData(token.image);
		} else if(queryData != null){
			returnValue = new QueryRelationData(queryData);
		}
		return returnValue;
    }
}

String WhereClause():
{
	String returnValue;
}
{
	(
    	"WHERE"
    	returnValue = SQLExpression()
    )
    {
    	return returnValue;
    }
}