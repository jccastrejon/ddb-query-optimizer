options {
	STATIC=false;
    MULTI=true;
    NODE_PACKAGE="mx.itesm.ddb.parser";
    TRACK_TOKENS=true;
    IGNORE_CASE=true;
    VISITOR=true;
}

PARSER_BEGIN(SqlParser)

package mx.itesm.ddb.parser; 

import java.util.List;
import java.util.ArrayList;
 
import mx.itesm.ddb.util.ConditionData;
import mx.itesm.ddb.util.ConditionOperator;
import mx.itesm.ddb.util.ExpressionData;
import mx.itesm.ddb.util.ExpressionOperator;
import mx.itesm.ddb.util.QueryData;
import mx.itesm.ddb.util.RelationData;

import mx.itesm.ddb.util.impl.AliasExpressionData;
import mx.itesm.ddb.util.impl.ConditionExpressionData;
import mx.itesm.ddb.util.impl.ExpressionConditionData;
import mx.itesm.ddb.util.impl.OperationConditionData;
import mx.itesm.ddb.util.impl.OperationExpressionData;
import mx.itesm.ddb.util.impl.QueryConditionData;
import mx.itesm.ddb.util.impl.QueryExpressionData;
import mx.itesm.ddb.util.impl.QueryRelationData;
import mx.itesm.ddb.util.impl.SimpleExpressionData;
import mx.itesm.ddb.util.impl.SimpleRelationData;

/**
 * SQL Parser.
 * 
 * @author jccastrejon
 * 
 */ 
public class SqlParser {
}

PARSER_END(SqlParser)

/* White spaces */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* SQL Select keywords */
TOKEN :
{
	<K_ALL:"ALL">
|   <K_AND:"AND">
|   <K_ANY:"ANY">
|   <K_AS:"AS">
|   <K_ASC:"ASC">
|	<K_BETWEEN:"BETWEEN">
|   <K_DESC:"DESC">
|   <K_DISTINCT:"DISTINCT">
|   <K_EXISTS:"EXISTS">
|   <K_FROM:"FROM">
|	<K_IN:"IN">
|	<K_IS:"IS">
|   <K_LIKE:"LIKE">
|   <K_NOT:"NOT">
|   <K_NULL:"NULL">
|   <K_OR:"OR">
|   <K_SELECT:"SELECT">
|   <K_WHERE:"WHERE">
}

/* Numeric constants */
TOKEN :
{
	< S_NUMBER: <FLOAT>
	    | <FLOAT> ( ["e","E"] ([ "-","+"])? <FLOAT> )?>
| 	< #FLOAT: <INTEGER>
	    | <INTEGER> ( "." <INTEGER> )?
	    | "." <INTEGER>>
| 	< #INTEGER: ( <DIGIT> )+ >
| 	< #DIGIT: ["0" - "9"] >
}

/* Identifiers */
TOKEN:
{
    < S_IDENTIFIER: (<LETTER>)+ (<DIGIT> | <LETTER> |<SPECIAL_CHARS>)* >
|	< #LETTER: ["a"-"z", "A"-"Z"] >
|	< #SPECIAL_CHARS: "$" | "_" | "#">
|	< S_CHAR_LITERAL: "'" (~["'"])* "'" ("'" (~["'"])* "'")*>
}

/**
 * Represents a SQL code block.
 */
QueryData QueryStatement():
{
	QueryData returnValue;
}
{
	returnValue = Query()
	";"
    {
    	return returnValue;
    }
}

/**
 * SQL query.
 */
QueryData Query() :
{
	QueryData returnValue;
	List<ExpressionData> attributes;
	List<RelationData> relations;
	ConditionData conditions = null;
}
{
    "SELECT" [ "ALL" | "DISTINCT" ] 
    attributes = SelectList()
    relations = FromClause()
    [
    	conditions = WhereClause()
    ]
    {
    	returnValue = new QueryData(attributes, relations, conditions);
    	return returnValue;
    }
}

/*
 * Result attributes.
 */
List<ExpressionData> SelectList():
{
	List<ExpressionData> returnValue = new ArrayList<ExpressionData>();
	ExpressionData expression;	
}
{
	(
	    "*"
	    {
	    	returnValue.add(new SimpleExpressionData("*"));
	    } 
	    | expression = SelectItem()
	    {
	    	returnValue.add(expression);
	    }
	   	(
	   		"," 
	   		expression = SelectItem()
	   		{
	   			returnValue.add(expression);
	   		}
	   	)*
   	)
    {
    	return returnValue;
    }
}

ExpressionData SelectItem():
{
	ExpressionData returnValue;
	Token token = null;
}
{
	(
	    (
			LOOKAHEAD(2) token = <S_IDENTIFIER>".*" // table.*
			{
				returnValue = new SimpleExpressionData(token.image + ".*");
			}
			| returnValue = SQLSimpleExpression() // column name or expression
	    )
	    [ 
	    	[
	    		"AS"
	    	]
	    	token = <S_IDENTIFIER>
	    	{
	    		returnValue = new AliasExpressionData(returnValue, token.image);
	    	}
	    ]
	)
	{
		return returnValue;
	}
}

ExpressionData SQLSimpleExpression():
{
	ExpressionData returnValue;
	ExpressionData firstExpression;
	ExpressionData secondExpression;
	ExpressionOperator operator;
}
{
	(
		firstExpression = SQLMultiplicativeExpression()
		{
			returnValue = firstExpression;
		}
		(
			(
				"+"
				{
					operator = ExpressionOperator.ArithmeticOperator.ADD_OPERATOR;
				}
				| "-"
				{
					operator = ExpressionOperator.ArithmeticOperator.SUBSTRACT_OPERATOR;
				}
			) 
			secondExpression = SQLMultiplicativeExpression()
			{
				returnValue = new OperationExpressionData(operator, firstExpression, secondExpression);
				firstExpression = returnValue;
			}
		)*
	)
	{
		return returnValue;
	}
}


ExpressionData SQLMultiplicativeExpression():
{
	ExpressionData returnValue;
	ExpressionData firstExpression;
	ExpressionData secondExpression;
	ExpressionOperator operator;
}
{
	(
		firstExpression = SQLUnaryExpression()
		{
			returnValue = firstExpression;
		}
		(
			(
				"*"
				{
					operator = ExpressionOperator.ArithmeticOperator.MULTIPLY_OPERATOR;
				}
				| "/"
				{
					operator = ExpressionOperator.ArithmeticOperator.DIVIDE_OPERATOR;
				}
				| "="							//TODO: Is this correct here?:.
				{
					operator = ExpressionOperator.ArithmeticOperator.EQUALS_OPERATOR;
				}
			)
			secondExpression = SQLUnaryExpression()
			{
				returnValue = new OperationExpressionData(operator, firstExpression, secondExpression);
				firstExpression = returnValue;
			}
		)*
	)
	{
		return returnValue;
	}
}

ExpressionData SQLUnaryExpression():
{
	ExpressionData returnValue;
	ExpressionData expression;
	ExpressionOperator operator = null;
}
{
	(
	    [
	    	"+"
	    	{
	    		operator = ExpressionOperator.ArithmeticOperator.ADD_OPERATOR;
	    	}
	    	| "-"
	    	{
	    		operator = ExpressionOperator.ArithmeticOperator.SUBSTRACT_OPERATOR;
	    	}
	    ] 
	    expression = SQLPrimaryExpression()
	    {
	    	returnValue = expression;
	    }
	)
	{
		if(operator != null){
			returnValue = new OperationExpressionData(operator, expression);	
		}
		
		return returnValue;
	}
}

ExpressionData SQLPrimaryExpression():
{
	ExpressionData returnValue = null;
	String simpleExpression = null;
	Token token;
	QueryData queryData;
	ConditionData conditionData;
}
{
	(
	    token = <S_NUMBER>
	    {
	    	simpleExpression = token.image;
	    }
		| token = <S_CHAR_LITERAL>
	    {
	    	simpleExpression = token.image;
	    }
		| token = <S_IDENTIFIER>
	    {
	    	simpleExpression = token.image;
	    }
		| "NULL"
	    {
	    	simpleExpression = "NULL";
	    }
		| "("
			(
				LOOKAHEAD(3) queryData = Query()
				{
					returnValue = new QueryExpressionData(queryData);
				}
				| conditionData = SQLExpression()
				{
					returnValue = new ConditionExpressionData(conditionData);
				}
			)
		")"
	)
	{
		if(simpleExpression != null){
			returnValue = new SimpleExpressionData(simpleExpression);
		}
		return returnValue;
	}
}

ConditionData SQLExpression():
{
	ConditionData returnValue;
	ConditionData firstCondition;
	ConditionData secondCondition;
}
{
	(
	    firstCondition = SQLAndExpression()
	    {
	    	returnValue = firstCondition;
	    }
	    (
	    	"OR"
	    	secondCondition = SQLAndExpression()
	    	{
				returnValue = new OperationConditionData(ConditionOperator.BinaryOperator.OR_OPERATOR, 
					firstCondition, secondCondition);
				firstCondition = returnValue;
	    	}
	    )*
	)
	{
		return returnValue;
	}
}

ConditionData SQLAndExpression():
{
	ConditionData returnValue;
	ConditionData firstCondition;
	ConditionData secondCondition;
}
{
	(
	    firstCondition = SQLUnaryLogicalExpression()
	    {
	    	returnValue = firstCondition;
	    }
	    (
	    	"AND"
	    	secondCondition = SQLUnaryLogicalExpression()
	    	{
				returnValue = new OperationConditionData(ConditionOperator.BinaryOperator.AND_OPERATOR, 
					firstCondition, secondCondition);
				firstCondition = returnValue;
	    	}
	    )*
	)
	{
		return returnValue;
	}
}

ConditionData SQLUnaryLogicalExpression():
{
	ConditionData condition;
	ConditionOperator operator = null;
}
{
	(
	    LOOKAHEAD(2) condition = ExistsClause()
	    {
	    	operator = ConditionOperator.UnaryOperator.EXISTS_OPERATOR;
	    }
		|(
			[
				"NOT"
				{
					operator = ConditionOperator.UnaryOperator.NOT_OPERATOR;
				}
			]
			condition = SQLRelationalExpression()
		)
	)
	{
		return new OperationConditionData(operator, condition);
	}
}

ConditionData ExistsClause():
{	
	QueryData queryData;
	ConditionData returnValue;
	ConditionOperator notOperator = null;
	ConditionOperator existsOperator = null;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		notOperator = ConditionOperator.UnaryOperator.NOT_OPERATOR;
	    	}
	    ] 
	    "EXISTS"
	    {
	    	existsOperator = ConditionOperator.UnaryOperator.EXISTS_OPERATOR;
	    }
	    "("
	    queryData = Query()
	    ")"
	)
	{
		returnValue = new OperationConditionData(existsOperator, new QueryConditionData(queryData));
		
		if(notOperator != null){
			returnValue = new OperationConditionData(notOperator, returnValue);
		}
		
		return returnValue;
	}
}

ConditionData SQLRelationalExpression():
{
	ConditionData returnValue;
	ConditionData condition = null;
	ConditionOperator nullOperator = null;
	ExpressionData expression;
	List<ConditionData> conditions = null;
}
{
	(   
		(
			LOOKAHEAD(
				"("
				SQLSimpleExpression()
				","
			)
		
			"("
			returnValue = SQLExpressionList()
			")"
			| 
			(
				expression = SQLSimpleExpression()
				{
					returnValue = new ExpressionConditionData(expression);
				}
			)
		)
	    
	    /* Lookahead(2) is required because of NOT IN,NOT BETWEEN and NOT LIKE */
	    (
	        condition = SQLRelationalOperatorExpression()
	        | LOOKAHEAD(2) (condition = SQLInClause())
	        | LOOKAHEAD(2) (condition = SQLBetweenClause())
	        | LOOKAHEAD(2) (condition = SQLLikeClause())
	        | nullOperator = IsNullClause()
	    )?
	)
	{
		if(nullOperator != null) {
			returnValue = new OperationConditionData(nullOperator, returnValue);
		} else if (condition != null) {
			conditions = new ArrayList<ConditionData>();
	    	conditions.add(returnValue);
	    	conditions.addAll(((OperationConditionData)condition).getConditions());
	        returnValue = new OperationConditionData(((OperationConditionData)condition).getOperator(), conditions);
		}
		
		return returnValue;
	}
}

ConditionData SQLExpressionList():
{
	ConditionData returnValue;
	ConditionData firstCondition;
	ConditionData secondCondition;
}
{
	(
	    firstCondition = SQLExpression()
	    {
	    	returnValue = firstCondition;
	    }
	    (
	    	","
	    	secondCondition = SQLExpression()
	    	{
				returnValue = new OperationConditionData(ConditionOperator.BinaryOperator.COMMA_OPERATOR, 
					firstCondition, secondCondition);
				firstCondition = returnValue;
	    	}
	    )*
	)
	{
		return returnValue;
	}
}

ConditionData SQLRelationalOperatorExpression():
{
	ConditionData returnValue;
	ConditionOperator operator = null;
	QueryData queryData;
}
{
    /* Only after seeing an ANY/ALL or "(" followed by a SubQuery() we can
    determine that is is a sub-query
    */
    (   LOOKAHEAD(
			"ANY"
			| "ALL"
			| "(" "SELECT"
    	)
        (
        	[
        		"ALL"
        		{
        			operator = ConditionOperator.UnaryOperator.ALL_OPERATOR;
        		}
        		| "ANY"
        		{
        			operator = ConditionOperator.UnaryOperator.ANY_OPERATOR;
        		}
        	] 
        	"("
        	queryData = Query()
        	{
        		returnValue = new QueryConditionData(queryData);
        	} 
        	")"
        )
    )
	{
		if(operator != null) {
			returnValue = new OperationConditionData(operator, returnValue);
		}
		
		return returnValue;
	}
}

ConditionData SQLInClause():
{
	ConditionData returnValue;
	ConditionOperator notOperator = null;
	ConditionOperator inOperator = null;
	QueryData queryData;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		notOperator = ConditionOperator.UnaryOperator.NOT_OPERATOR;
	    	}
	    ] 
	    "IN"
	    {
	    	inOperator = ConditionOperator.BinaryOperator.IN_OPERATOR;
	    } 
	    "("
	    (
	    	LOOKAHEAD(3) queryData = Query()
	    	{
	    		returnValue = new QueryConditionData(queryData);
	    	} 
	    	| returnValue = SQLExpressionList()
	    ) 
	    ")"
	)
	{
		returnValue = new OperationConditionData(inOperator, returnValue);
		
		if(notOperator != null) {
			returnValue = new OperationConditionData(notOperator, returnValue);
		}
		
		return returnValue;
	}
}

ConditionData SQLBetweenClause():
{
	ConditionData returnValue;
	ConditionData firstCondition;
	ConditionData secondCondition;
	ExpressionData expression;
	ConditionOperator notOperator = null;
	ConditionOperator betweenOperator = null;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		notOperator = ConditionOperator.UnaryOperator.NOT_OPERATOR;
	    	}
	    ]
	    "BETWEEN"
	    {
	    	betweenOperator = ConditionOperator.BinaryOperator.BETWEEN_OPERATOR;
	    } 
	    expression = SQLSimpleExpression()
	    {
	    	firstCondition = new ExpressionConditionData(expression);
	    } 
	    "AND"
	    expression = SQLSimpleExpression()
	    {
	    	secondCondition = new ExpressionConditionData(expression);
	    }
	)
	{
		returnValue = new OperationConditionData(betweenOperator, firstCondition, secondCondition);
		
		if(notOperator != null) {
			returnValue = new OperationConditionData(notOperator, returnValue);
		}
		
		return returnValue;
	}
}

ConditionData SQLLikeClause():
{
	ConditionData returnValue;
	ExpressionData expression;
	ExpressionConditionData expressionCondition;
	ConditionOperator notOperator = null;
	ConditionOperator likeOperator = null;
}
{
	(
	    [
	    	"NOT"
	    	{
	    		notOperator = ConditionOperator.UnaryOperator.NOT_OPERATOR;
	    	}
	    ] 
	   	"LIKE"
	   	{
	   		likeOperator = ConditionOperator.BinaryOperator.LIKE_OPERATOR;
	   	}
	   	expression = SQLSimpleExpression()
	   	{
	   		expressionCondition = new ExpressionConditionData(expression);
	   	}
	)
	{
		returnValue = new OperationConditionData(likeOperator, expressionCondition);
		
		if(notOperator != null) {
			returnValue = new OperationConditionData(notOperator, returnValue);
		}
		
		return returnValue;
	}
}

ConditionOperator IsNullClause():
{
	ConditionOperator returnValue;
}
{
	(
	    "IS"
	    {
	    	returnValue = ConditionOperator.UnaryOperator.IS_NULL_OPERATOR;
	    }
	    [
	    	"NOT"
	    	{
	    		returnValue = ConditionOperator.UnaryOperator.IS_NOT_NULL_OPERATOR;
	    	}
	    ]
	    "NULL"
	)
	{	
		return returnValue;
	}
}

List<RelationData> FromClause():
{
	List<RelationData> returnValue = new ArrayList<RelationData>();
	RelationData relation;
}
{
    "FROM" 
    relation = QueryTableExpression()
    {
    	returnValue.add(relation);
    }
    ( 
    	"," relation = QueryTableExpression()
    	{
    		returnValue.add(relation);
    	}
    )*
    {
    	return returnValue;
    }
}

RelationData QueryTableExpression():
{
	RelationData returnValue = null;
	Token token = null;
	QueryData queryData = null;
}
{
    (
		token = <S_IDENTIFIER>
		| LOOKAHEAD(3) "(" queryData = Query() ")"
    )
    {
		if(token != null){
			returnValue = new SimpleRelationData(token.image);
		} else if(queryData != null){
			returnValue = new QueryRelationData(queryData);
		}
		return returnValue;
    }
}

ConditionData WhereClause():
{
	ConditionData returnValue;
}
{
	(
    	"WHERE"
    	returnValue = SQLExpression()
    )
    {
    	return returnValue;
    }
}